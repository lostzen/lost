buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:2.3.2'
    classpath 'com.github.dcendents:android-maven-gradle-plugin:1.3'
    classpath 'net.researchgate:gradle-release:2.4.0'
  }
}

apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'checkstyle'
apply plugin: 'net.researchgate.release'

group = GROUP
version = VERSION_NAME

allprojects {
  repositories {
    mavenLocal()
    jcenter()
  }
}

release {
  tagTemplate = 'lost-${version}'
  versionPropertyFile = '../gradle.properties'
  versionProperties = ['VERSION_NAME']
}

android {
  compileSdkVersion 25
  buildToolsVersion '25.0.3'

  defaultConfig {
    minSdkVersion 15
    targetSdkVersion 25
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_7
    targetCompatibility JavaVersion.VERSION_1_7
  }

  packagingOptions {
    exclude 'META-INF/LICENSE.txt'
  }

  lintOptions {
    abortOnError false
  }
}

repositories {
  jcenter()
}

tasks.withType(Test) {
  testLogging {
    exceptionFormat "full"
    events "started", "skipped", "passed", "failed"
    showStandardStreams true
  }
}

task checkstyle(type: Checkstyle) {
  configFile file("${project.rootDir}/config/checkstyle/checkstyle.xml")
  source 'src'
  include '**/*.java'
  include '**/*.kt'
  exclude '**/gen/**'

  classpath = files()
}

task javadocs(type: Javadoc) {
  classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
  source = android.sourceSets.main.java.srcDirs
  exclude '**/internal/*.java'
  destinationDir = file("../gh-pages")
}

afterEvaluate {
  javadocs.classpath += files(android.libraryVariants.collect { variant ->
    variant.javaCompile.classpath.files
  })
}

task printVersion << {
  println version
}

dependencies {
  compile 'com.android.support:support-annotations:25.3.1'
  compile 'com.android.support:appcompat-v7:25.3.1'

  testCompile 'com.google.guava:guava:18.0'
  testCompile 'junit:junit:4.12'
  testCompile 'org.robolectric:robolectric:3.2.2'
  testCompile 'com.squareup:fest-android:1.0.7'
  testCompile 'org.mockito:mockito-core:1.9.5'
}

apply from: file('../gradle-mvn-push.gradle')
